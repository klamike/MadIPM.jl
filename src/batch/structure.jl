struct UniformBatchWorkspace{T, VT<:AbstractVector{T}, MT<:AbstractMatrix{T}, MI<:AbstractMatrix{Int32}, MI64<:AbstractMatrix{Int64}}
    alpha_xl::MT
    alpha_zl::MT
    alpha_p::MT
    alpha_d::MT
    tau::MT

    mu_batch::MT
    mu_curr::MT
    mu_affine::MT
    sum_lb::MT

    obj_val::MT
    norm_b::MT
    norm_c::MT
    inf_pr::MT
    inf_du::MT
    inf_compl::MT
    best_complementarity::MT
    dual_obj::MT
    status::Vector{MadNLP.Status}

    _term_gpu::MI64
    _term_cpu::Matrix{Int64}
    _any_nonregular_gpu::MI64
    _any_nonregular_cpu::Matrix{Int64}
    _norm_gpu_w::MT
    _norm_gpu_p::MT
    _ls_error::MI

    active_mask::MT
    active_mask_cpu::Matrix{T}

    bx::MT
    bf::VT
    bg::MT
    bv::MT
end

function UniformBatchWorkspace(::Type{MT}, ::Type{VT}, n::Int, m::Int, nlb::Int, batch_size::Int;
                        nvar_nlp::Int=0) where {T, MT<:AbstractMatrix{T}, VT<:AbstractVector{T}}
    _proto = MT(undef, 1, batch_size)
    MI = typeof(similar(_proto, Int32))
    MI64 = typeof(similar(_proto, Int64))
    return UniformBatchWorkspace{T, VT, MT, MI, MI64}(
        MT(undef, 1, batch_size),  # alpha_xl
        MT(undef, 1, batch_size),  # alpha_zl
        MT(undef, 1, batch_size),  # alpha_p
        MT(undef, 1, batch_size),  # alpha_d
        MT(undef, 1, batch_size),  # tau
        MT(undef, 1, batch_size),  # mu_batch
        MT(undef, 1, batch_size),  # mu_curr
        MT(undef, 1, batch_size),  # mu_affine
        MT(undef, 1, batch_size),  # sum_lb
        MT(undef, 1, batch_size),  # obj_val
        MT(undef, 1, batch_size),  # norm_b
        MT(undef, 1, batch_size),  # norm_c
        MT(undef, 1, batch_size),  # inf_pr
        MT(undef, 1, batch_size),  # inf_du
        MT(undef, 1, batch_size),  # inf_compl
        MT(undef, 1, batch_size),  # best_complementarity
        MT(undef, 1, batch_size),  # dual_obj
        fill(MadNLP.INITIAL, batch_size),  # status
        similar(_proto, Int64, 1, batch_size),    # _term_gpu
        zeros(Int64, 1, batch_size),  # _term_cpu
        fill!(similar(_proto, Int64, 1, 1), Int64(MadNLP.REGULAR)),  # _any_nonregular_gpu
        fill(Int64(MadNLP.REGULAR), 1, 1),  # _any_nonregular_cpu
        MT(undef, 1, batch_size),  # _norm_gpu_w
        MT(undef, 1, batch_size),  # _norm_gpu_p
        fill!(similar(_proto, Int32), zero(Int32)),  # _ls_error
        fill!(MT(undef, 1, batch_size), one(T)),  # active_mask
        ones(T, 1, batch_size),                    # active_mask_cpu
        MT(undef, nvar_nlp, batch_size),   # bx
        VT(undef, batch_size),  # bf
        MT(undef, nvar_nlp, batch_size),   # bg
        MT(undef, m, batch_size),          # bv
    )
end

mutable struct UniformBatchMPCSolver{T, MT, VT, VI, BM, BCB, BVS, KKT<:AbstractBatchKKTSystem{T}, REG<:AbstractRegularization, STEP<:AbstractStepRule, BARR<:AbstractBarrierUpdate} <: AbstractBatchMPCSolver{T, MT, VT}
    batch_size::Int

    d::BatchUnreducedKKTVector{T, MT}
    p::BatchUnreducedKKTVector{T, MT}
    _w1::BatchUnreducedKKTVector{T, MT}

    x::BatchPrimalVector{T, MT}
    xl::BatchPrimalVector{T, MT}
    zl::BatchPrimalVector{T, MT}
    f::BatchPrimalVector{T, MT}

    y::BatchVector{T, MT}
    c::BatchVector{T, MT}
    jacl::BatchVector{T, MT}
    rhs::BatchVector{T, MT}
    correction_lb::BatchVector{T, MT}

    workspace::UniformBatchWorkspace{T, VT, MT}

    opt::IPMOptions
    regularization::REG
    step_rule::STEP
    barrier_update::BARR
    batch_cnt::BatchCounters
    logger::MadNLP.MadNLPLogger
    batch_views::BVS
    kkt::KKT

    del_w::MT
    del_c::MT

    nlp::BM
    bcb::BCB
end

# UB fields don't exist on UniformBatchMPCSolver; return empty placeholders
# via `getproperty` so legacy batch code that still reads `solver.xu` /
# `solver.zu` / `solver.correction_ub` (inert on std-form input) stays valid.
Base.getproperty(bs::UniformBatchMPCSolver, s::Symbol) =
    s === :xu ? getfield(bs, :x) :
    s === :zu ? getfield(bs, :zl) :
    s === :correction_ub ? getfield(bs, :correction_lb) :
    getfield(bs, s)

_get_ind_lb(bs::AbstractBatchMPCSolver) = bs.bcb.ind_lb
_get_ind_ub(bs::AbstractBatchMPCSolver) = bs.bcb.ind_lb[1:0]
active_batch_size(bs::AbstractBatchMPCSolver) = local_batch_size(active_view(bs.batch_views))

function update_active_set!(state::BatchViewState, status::Vector{MadNLP.Status})
    nselected = 0
    @inbounds for i in eachindex(status)
        if status[i] == MadNLP.REGULAR
            nselected += 1
            state.selected_local_buffer[nselected] = i
        end
    end
    if nselected == batch_size_root(root_view(state))
        return reset_active_view!(state)
    end
    reset_active_view!(state)
    return select_local!(state, state.selected_local_buffer, nselected; reset_slots=true)
end

update_active_set!(bs::AbstractBatchMPCSolver) = update_active_set!(bs.batch_views, bs.workspace.status)


"""
    UniformBatchMPCSolver(bnlp::AbstractBatchNLPModel; linear_solver, kwargs...)

Construct a batch solver from a standard-form `AbstractBatchNLPModel`.
"""
function UniformBatchMPCSolver(
    bnlp::NLPModels.AbstractBatchNLPModel{T, MT};
    VT = typeof(similar(NLPModels.get_x0(bnlp), T, 0)),
    VI = typeof(similar(NLPModels.get_x0(bnlp), Int, 0)),
    uniformbatch_linear_solver = LoopedBatchLinearSolver,
    check_batch_structure::Bool = true,
    kwargs...,
) where {T, MT}
    bmeta = bnlp.meta
    batch_size = bmeta.nbatch
    @assert batch_size > 0 "Need at least one instance in batch"

    nvar_nlp = bmeta.nvar

    opt_batch_ls = MadNLP.default_options(uniformbatch_linear_solver)
    remaining_kwargs = MadNLP.set_options!(opt_batch_ls, kwargs)

    options = load_options(bnlp; remaining_kwargs...)
    ipm_opt = options.interior_point
    logger = options.logger
    regularization = options.regularization
    step_rule = options.step_rule
    barrier_update = options.barrier_update

    batch_cnt = BatchCounters(batch_size)
    bcb = MadNLP.create_callback(
        UniformBatchCallback{T,VT,MT,VI},
        bnlp;
        fixed_variable_treatment=ipm_opt.fixed_variable_treatment,
        equality_treatment=ipm_opt.equality_treatment,
        check_batch_structure=check_batch_structure,
    )

    ind_lb = bcb.ind_lb

    ns = length(bcb.ind_ineq)
    nx = bcb.nvar
    n = nx + ns
    m = bcb.ncon
    nlb = length(ind_lb)

    batch_views = BatchViewState(bcb, batch_size)

    batch_kkts = MadNLP.create_kkt_system(
        ipm_opt.kkt_system,
        bcb,
        uniformbatch_linear_solver;
        opt_linear_solver = opt_batch_ls,
        batch_views = batch_views,
    )

    batch_x  = BatchPrimalVector(MT, VT, nx, ns, batch_size, ind_lb)
    batch_xl = BatchPrimalVector(MT, VT, nx, ns, batch_size, ind_lb)
    batch_zl = BatchPrimalVector(MT, VT, nx, ns, batch_size, ind_lb)
    batch_f  = BatchPrimalVector(MT, VT, nx, ns, batch_size, ind_lb)

    batch_d  = BatchUnreducedKKTVector(MT, VT, n, m, nlb, batch_size, ind_lb)
    batch_p  = BatchUnreducedKKTVector(MT, VT, n, m, nlb, batch_size, ind_lb)
    batch_w1 = BatchUnreducedKKTVector(MT, VT, n, m, nlb, batch_size, ind_lb)

    batch_correction_lb = BatchVector(MT, VT, nlb, batch_size)
    batch_jacl          = BatchVector(MT, VT, n, batch_size)
    batch_y             = BatchVector(MT, VT, m, batch_size)
    batch_c             = BatchVector(MT, VT, m, batch_size)
    batch_rhs           = BatchVector(MT, VT, m, batch_size)

    workspace = UniformBatchWorkspace(MT, VT, n, m, nlb, batch_size;
                               nvar_nlp=nvar_nlp)

    batch_del_w = fill!(MT(undef, 1, batch_size), zero(T))
    batch_del_c = fill!(MT(undef, 1, batch_size), zero(T))

    return UniformBatchMPCSolver{T, MT, VT, VI, typeof(bnlp), typeof(bcb), typeof(batch_views), typeof(batch_kkts), typeof(regularization), typeof(step_rule), typeof(barrier_update)}(
        batch_size,
        batch_d, batch_p, batch_w1,
        batch_x, batch_xl, batch_zl, batch_f,
        batch_y, batch_c, batch_jacl, batch_rhs,
        batch_correction_lb,
        workspace,
        ipm_opt, regularization, step_rule, barrier_update,
        batch_cnt, logger,
        batch_views,
        batch_kkts,
        batch_del_w, batch_del_c,
        bnlp,
        bcb,
    )
end
